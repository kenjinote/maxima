@c
@c versao pt_BR baseada no md5sum abaixo:
@c 0ca72bd067ec8cfe8ebf26f085a9a6eb  Introduction.texi
@c
Inicie o Maxima com o comando "maxima" em uma janela de shell.  Maxima ir@'{a} mostrar informa@,{c}@~{o}es
de vers@~{a}o e um prompt.  Termine cada comando do Maxima com um ponto e v@'{i}rgula.
Encerre a utiliza@,{c}@~{a}o do Maxima com o comando "quit();".  Adiante temos uma amostra de sess@~{a}o:

@example
[wfs@@chromium]$ maxima
Maxima 5.9.1 http://maxima.sourceforge.net
Using Lisp CMU Common Lisp 19a
Distributed under the GNU Public License. See the file COPYING.
Dedicated to the memory of William Schelter.
This is a development version of Maxima. The function bug_report()
provides bug reporting information.
(%i1) factor(10!);
                            8  4  2
(%o1)                      2  3  5  7
(%i2) expand ((x + y)^6);
       6        5       2  4       3  3       4  2      5      6
(%o2) y  + 6 x y  + 15 x  y  + 20 x  y  + 15 x  y  + 6 x  y + x
(%i3) factor (x^6 - 1);
                              2            2
(%o3)       (x - 1) (x + 1) (x  - x + 1) (x  + x + 1)
(%i4) quit();
[wfs@@chromium]$
@end example

Maxima pode buscar p@'{a}ginas de manual no formato info.  Use o comando @mref{describe}
para mostrar
informa@,{c}@~{a}o sotre o comando ou todos os comandos e vari@'{a}veis contendo
uma sequ@^{e}ncia de caracteres.
O ponto de interroga@,{c}@~{a}o simples @mref{?}
(busca exata) e a dupla interroga@,{c}@~{a}o @mref{??}
@w{} (busca inexata) s@~{a}o abrevia@,{c}@~{o}es para @code{describe}:

@example
(%i1) ?? integ

 0: Exponential Integrals
 1: Functions and Variables for Elliptic Integrals
 2: Functions and Variables for Integration
 3: Introduction to Elliptic Functions and Integrals
 4: Introduction to Integration
 5: askinteger  (Functions and Variables for Facts)
 6: beta_args_sum_to_integer  (Gamma and factorial Functions)
 7: expintegral_chi  (Exponential Integrals)
 8: expintegral_ci  (Exponential Integrals)
 9: expintegral_e  (Exponential Integrals)
 10: expintegral_e1  (Exponential Integrals)
 11: expintegral_ei  (Exponential Integrals)
 12: expintegral_li  (Exponential Integrals)
 13: expintegral_shi  (Exponential Integrals)
 14: expintegral_si  (Exponential Integrals)
 15: integerp  (Functions and Variables for Numbers)
 16: integer_partitions  (Functions and Variables for Sets)
 17: integrate  (Functions and Variables for Integration)
 18: integrate_use_rootsof  (Functions and Variables for Integration)
 19: integration_constant  (Functions and Variables for Integration)
 20: integration_constant_counter  (Functions and Variables for Integration)
 21: nonnegintegerp  (Functions and Variables for Numbers)
Enter space-separated numbers, `all' or `none': 15 5
 -- Function: integerp (<expr>)
     Returns `true' if <expr> is a literal numeric integer, otherwise
     `false'.

     `integerp' returns false if its argument is a symbol, even if the
     argument is declared integer.

     Examples:

          (%i1) integerp (0);
          (%o1)                         true
          (%i2) integerp (1);
          (%o2)                         true
          (%i3) integerp (-17);
          (%o3)                         true
          (%i4) integerp (0.0);
          (%o4)                         false
          (%i5) integerp (1.0);
          (%o5)                         false
          (%i6) integerp (%pi);
          (%o6)                         false
          (%i7) integerp (n);
          (%o7)                         false
          (%i8) declare (n, integer);
          (%o8)                         done
          (%i9) integerp (n);
          (%o9)                         false

 -- Function: askinteger (<expr>, integer)
 -- Function: askinteger (<expr>)
 -- Function: askinteger (<expr>, even)
 -- Function: askinteger (<expr>, odd)
     `askinteger (<expr>, integer)' attempts to determine from the
     `assume' database whether <expr> is an integer.  `askinteger'
     prompts the user if it cannot tell otherwise, and attempt to
     install the information in the database if possible.  `askinteger
     (<expr>)' is equivalent to `askinteger (<expr>, integer)'.

     `askinteger (<expr>, even)' and `askinteger (<expr>, odd)'
     likewise attempt to determine if <expr> is an even integer or odd
     integer, respectively.

(%o1)                                true
@end example

Para usar um resultado em c@'{a}lculos posteriores, voc@^{e} pode atribuir esse resultado a uma vari@'{a}vel ou
fazer uma refer@^{e}ncia a exxe c@'{a}lculo anterior por meio de seu r@'{o}tulo fornecido automaticamente.  Adicionalmente, @mref{%}
@w{}(sinal de porcentagem) refere-se ao resultado calculado mais recentemente:

@example
(%i1) u: expand ((x + y)^6);
       6        5       2  4       3  3       4  2      5      6
(%o1) y  + 6 x y  + 15 x  y  + 20 x  y  + 15 x  y  + 6 x  y + x
(%i2) diff (u, x);
         5         4       2  3       3  2       4        5
(%o2) 6 y  + 30 x y  + 60 x  y  + 60 x  y  + 30 x  y + 6 x
(%i3) factor (%o2);
                                    5
(%o3)                      6 (y + x)
@end example

Maxima conhece n@'{u}meros complexos e constantes matem@'{a}ticas:

@example
(%i1) cos(%pi);
(%o1)                          - 1
(%i2) exp(%i*%pi);
(%o2)                          - 1
@end example

Maxima pode fazer c@'{a}lculos difereciais e integrais:

@example
(%i1) u: expand ((x + y)^6);
       6        5       2  4       3  3       4  2      5      6
(%o1) y  + 6 x y  + 15 x  y  + 20 x  y  + 15 x  y  + 6 x  y + x
(%i2) diff (%, x);
         5         4       2  3       3  2       4        5
(%o2) 6 y  + 30 x y  + 60 x  y  + 60 x  y  + 30 x  y + 6 x
(%i3) integrate (1/(1 + x^3), x);
                                  2 x - 1
                2            atan(-------)
           log(x  - x + 1)        sqrt(3)    log(x + 1)
(%o3)    - --------------- + ------------- + ----------
                  6             sqrt(3)          3
@end example

Maxima pode resolver sistemas lineares e equa@,{c}@~{o}es c@'{u}bicas:

@example
(%i1) linsolve ([3*x + 4*y = 7, 2*x + a*y = 13], [x, y]);
                        7 a - 52        25
(%o1)              [x = --------, y = -------]
                        3 a - 8       3 a - 8
(%i2) solve (x^3 - 3*x^2 + 5*x = 15, x);
(%o2)       [x = - sqrt(5) %i, x = sqrt(5) %i, x = 3]
@end example

Maxima pode resolver conjuntos de equa@,{c}@~{o}es n@~{a}o lineares.  Note que se voc@^{e} n@~{a}o
deseja que um resultado seja mostrado, voc@^{e} pode terminar seu comando com @kbd{$} ao inv@'{e}s de
terminar com @kbd{;}.

@example
(%i1) eq_1: x^2 + 3*x*y + y^2 = 0$
(%i2) eq_2: 3*x + y = 1$
(%i3) solve ([eq_1, eq_2]);
              3 sqrt(5) + 7      sqrt(5) + 3
(%o3) [[y = - -------------, x = -----------], 
                    2                 2

                               3 sqrt(5) - 7        sqrt(5) - 3
                          [y = -------------, x = - -----------]]
                                     2                   2
@end example

Maxima pode gerar gr@'{a}ficos gen@'{e}ricos de uma ou mais fun@,{c}@~{o}es:

@example
(%i1) plot2d (sin(x)/x, [x, -20, 20])$
@end example
@ifnotinfo
@image{figures/introduction1, 10cm}
@end ifnotinfo
@example
(%i2) plot2d ([atan(x), erf(x), tanh(x)], [x, -5, 5], [y, -1.5, 2])$
@end example
@ifnotinfo
@image{figures/introduction2, 10cm}
@end ifnotinfo
@example
@group
(%i3) plot3d (sin(sqrt(x^2 + y^2))/sqrt(x^2 + y^2), 
         [x, -12, 12], [y, -12, 12])$
@end group
@end example
@ifnotinfo
@image{figures/introduction3, 12cm}
@end ifnotinfo 

@c FOLLOWING TEXT DESCRIBES THE TCL/TK PLOT WINDOW WHICH IS NO LONGER THE DEFAULT
@c Moving the cursor to the top left corner of the plot window will pop up
@c a menu that will, among other things, let you generate a PostScript file
@c of the plot.  (By default, the file is placed in your home directory.)
@c You can rotate a 3D plot.

@opencatbox
@category{Ajuda}
@closecatbox

